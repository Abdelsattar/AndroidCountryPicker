apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '0.0.1-alpha20'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}
ext {
    libraryName = 'AndroidCountryPicker'

    bintrayRepo = 'maven'
    bintrayName = libraryName

    publishedGroupId = 'com.hbb20'
    artifactId = libraryName

    libraryDescription = 'Android Country Picker (CP) is an android library which provides an easy way to search and select country.'

    siteUrl = 'https://github.com/hbb20/AndroidCountryPicker'
    gitUrl = 'https://github.com/hbb20/AndroidCountryPicker.git'

    libraryVersion = this.version

    developerId = 'hbb20'
    developerName = 'Harsh Bhakta'
    developerEmail = 'harshbbhakta@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/hbb20/AndroidCountryPicker/blob/develop/LICENSE'
    allLicenses = ["MIT"]
}

def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
            distribution "repo"
        }
    }
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }
    scm {
        url siteUrl
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
afterEvaluate {
    publishing {
        publications
                {
                    AndroidCountryPicker(MavenPublication)
                            {
                                from components.release
                                groupId 'com.hbb20'
                                artifactId this.artifactId
                                version libraryVersion
                                artifact sourcesJar {
                                    classifier "sources"
                                }
                                pom.withXml {
                                    def root = asNode()
                                    root.appendNode("url", siteUrl)
                                    root.children().last() + pomConfig
                                }
                            }

                }
        repositories {
            // this works for upload but then need to publish manually
            // it is because we need to send a flag to publish when we upload as param or header of upload request
            // https://bintray.com/docs/api/#_upload_content
            // since MavenPublish does not provide any such way, we will continue using BintrayUpload
            // once we can get that then this will be the cleanest way to publish
            maven {
                name 'Bintray'
                url "https://api.bintray.com/maven/hbb20/maven/AndroidCountryPicker/"

                credentials {
                    username properties.getProperty("bintray.user")
                    password properties.getProperty("bintray.apikey")
                }
            }
        }
    }

    bintray {
        user = properties.getProperty("bintray.user")
        key = properties.getProperty("bintray.apikey")
        publications = ["AndroidCountryPicker"]
        pkg {
            repo = bintrayRepo
            name = bintrayName
            desc = libraryDescription
            websiteUrl = siteUrl
            vcsUrl = gitUrl
            licenses = allLicenses
            publish = true
            publicDownloadNumbers = true
            version {
                name = libraryVersion
                desc = libraryDescription
                gpg {
                    sign = true //Determines whether to GPG sign the files. The default is false
                    passphrase = properties.getProperty("bintray.gpg.password")
                    //Optional. The passphrase for GPG signing'
                }
            }
        }
    }
}

android {
    compileSdkVersion 28

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    tasks.withType(Javadoc).all { enabled = false }
}

dependencies {
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation "io.mockk:mockk:1.9.3"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //epoxy
    ext.epoxyVersion = '3.9.0'
    implementation "com.airbnb.android:epoxy:$epoxyVersion"
    kapt "com.airbnb.android:epoxy-processor:$epoxyVersion"
    implementation 'androidx.emoji:emoji:1.0.0'
    implementation 'org.apache.commons:commons-csv:1.7'
}
